Value Filldown VRF (\S+)
Value Filldown MULTICAST_GROUP (\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})
Value Required RPF_SOURCE (\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})
Value RP (\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})
Value UPTIME (\d+d\d+h|\d+:\d+:\d+)
Value FLAGS (\S+)
Value INCOMING_INTERFACE (\S+)
Value UPSTREAM_JOINED_STATE (\S+)
Value UPSTREAM_RPT_JOINED_STATE (\S+)
Value List OUTGOING_INTERFACE_LIST (\S+)

Start
  ^VRF:${VRF}
  ^PIM
  ^RPF
  ^\s+M\s-
  ^Flags
  ^\s{4}[RWIHZANTVF]\s-
  ^(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}) -> Continue.Record
  ^${MULTICAST_GROUP}
  ^\s{2}(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}) -> Continue.Record
  ^\s{2}${RPF_SOURCE},\s+${UPTIME},\s+(RP\s${RP},\s+)?flags:\s+${FLAGS}
  ^\s{4}Incoming\sinterface:\s${INCOMING_INTERFACE}
  ^\s{4}Outgoing\sinterface\slist:
  ^\s{6}${OUTGOING_INTERFACE_LIST}$$
  ^\s{4}\S\s-> Continue
  ^\s{4}Interfaces\snot\sin\sOIL
  ^\s{6}\S+
  ^\s{4}Upstream\sjoined\sstate:\s${UPSTREAM_JOINED_STATE}
  ^\s{4}Upstream\sRPT\sjoined\sstate:\s${UPSTREAM_RPT_JOINED_STATE} -> Record
  ^\s{4}State\ssummarization\smacros:
  ^\s{6}Could
  ^\s{6}Register
  ^\s{6}Join
  ^\s{6}RPT\sjoin\sdesired
  ^\s{6}Prune
  ^\s{6}No\s+interfaces\s+in\s+(immediate|inherited)\s+olist
  ^.* -> Error

EOF
